      *%METADATA                                                       *
      * %TEXT Gestion des films - Format FMT5                          *
      *%EMETADATA                                                      *
     A          R FMT1                      CA03(03)
     A                                  1 32'Recherche de films'
     A                                      COLOR(WHT)
     A                                  1  3'FMT1'
     A                                      COLOR(PNK)
     A                                  1 71DATE
     A                                      COLOR(BLU)
     A                                      EDTCDE(Y)
     A                                  4  5'Recherche du film dont le numéro d-
     A                                      e DVD est égal à'
     A                                  6  5'(saisissez un numéro de DVD, puis -
     A                                      appuyez sur ENTREE)'
     A                                 24  3'F3=Exit'
     A                                      COLOR(BLU)
     A            CODDVD    R     D  I  4 56REFFLD(CODDVD DVD)
     A  40                                  ERRMSG('DVD verrouillé' 40)
     A  41                                  ERRMSG('Numéro de DVD inconnu' 41)
     A*
     A          R FMT2                      CA03(03)
     A                                  1 32'Recherche de films'
     A                                      COLOR(WHT)
     A                                  1  3'FMT2'
     A                                      COLOR(PNK)
     A                                  1 71DATE
     A                                      COLOR(BLU)
     A                                      EDTCDE(Y)
     A                                  4  5'Recherche des films dont le titre -
     A                                      contient'
     A                                  6  5'(saisissez un critère de recherche-
     A                                      , puis appuyez sur ENTREE)'
     A                                 24  3'F3=Exit'
     A                                      COLOR(BLU)
     A            RECHERCHE     15   I  4 48
     A  42                                  ERRMSG('Aucun film ne répond au cri-
     A                                      ère de recherche' 42)
     A*
     A          R FMT3                      CA03(03)
     A                                      CA10(10)
     A                                  1  2'FMT3'
     A                                      COLOR(PNK)
     A                                  1 33'Détails d''un film'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      COLOR(BLU)
     A                                      EDTCDE(Y)
     A            CODDVD    R     D  O  4 24REFFLD(CODDVD DVD)
     A            TITRE     R        O  5 24REFFLD(TITRE DVD)
     A            GENRE     R        O  6 24REFFLD(GENRE DVD)
     A            ANNEE     R     D  O  7 24REFFLD(ANNEE DVD)
     A            DUREE     R     D  O  8 24REFFLD(DUREE DVD)
     A                                  4 14'Code DVD'
     A                                      COLOR(WHT)
     A                                  5  9'Titre du film'
     A                                      COLOR(WHT)
     A                                  6 12'Code genre'
     A                                      COLOR(WHT)
     A                                  7 17'Année'
     A                                      COLOR(WHT)
     A                                  8 17'Durée'
     A                                      COLOR(WHT)
     A                                 24  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 24 13'F10=Détails'
     A                                      COLOR(BLU)
     A  43        DETAIL1       15   O  7 29COLOR(BLU)
     A*
     A          R FMT4                      CA03(03)
     A                                  1  2'FMT4'
     A                                      COLOR(PNK)
     A                                  1 31'Mise à jour d''un film'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      COLOR(BLU)
     A                                      EDTCDE(Y)
     A            CODDVD    R     D  O  4 24REFFLD(CODDVD DVD)
     A            TITRE     R        B  5 24REFFLD(TITRE DVD)
     A                                      CHECK(LC)
     A            GENRE     R        B  6 24REFFLD(GENRE DVD)
     A            ANNEE     R     D  B  7 24REFFLD(ANNEE DVD)
     A            DUREE     R     D  B  8 24REFFLD(DUREE DVD)
     A                                  4 14'Code DVD'
     A                                      COLOR(WHT)
     A                                  5  9'Titre du film'
     A                                      COLOR(WHT)
     A                                  6 12'Code genre'
     A                                      COLOR(WHT)
     A                                  7 17'Année'
     A                                      COLOR(WHT)
     A                                  8 17'Durée'
     A                                      COLOR(WHT)
     A                                 24  2'F3=Exit'
     A                                      COLOR(BLU)
     A
     A          R FMT5                      WINDOW(4 7 7 50)
     A                                  1  2'FMT5'
     A                                      COLOR(PNK)
     A                                  1 15'Confirmation de l''opération'
     A                                      COLOR(WHT)
     A                                  3  3'La mise à jour a été effectuée'
     A                                  5  3'Confirmez-vous l''opération ? (O/N-
     A                                      )'
     A                                      COLOR(BLU)
     A            REP            1   I  5 40VALUES('O' 'N')

