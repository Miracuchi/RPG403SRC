      *%METADATA                                                       *
      * %TEXT Gestion des films - Rappel avant M11 - Partie A          *
      *%EMETADATA                                                      *
     A          R FMT1                      CA03(03)
     A                                  1 32'Recherche de films'
     A                                      COLOR(WHT)
     A                                  1  3'FMT1'
     A                                      COLOR(PNK)
     A                                  1 71DATE
     A                                      COLOR(BLU)
     A                                      EDTCDE(Y)
     A                                  4  5'Recherche du film dont le numéro d-
     A                                      e DVD est égal à'
     A                                  6  5'(saisissez un numéro de DVD, puis -
     A                                      appuyez sur ENTREE)'
     A                                 24  3'F3=Exit'
     A                                      COLOR(BLU)
     A            CODDVD    R     D  I  4 56REFFLD(CODDVD DVD)
     A  41                                  ERRMSG('Numéro de DVD inconnu' 41)
     A*
     A          R FMT2                      CA12(12)
     A                                  1  2'FMT2'
     A                                      COLOR(PNK)
     A                                  1 33'Détails d''un film'
     A                                      COLOR(WHT)
     A                                  1 72DATE
     A                                      COLOR(BLU)
     A                                      EDTCDE(Y)
     A            CODEF     R     D  O  4 24REFFLD(CODDVD DVD)
     A            TITREF    R        O  5 24REFFLD(TITRE DVD)
     A            GENREF    R        O  6 24REFFLD(GENRE DVD)
     A            ANNEEF    R     D  O  7 24REFFLD(ANNEE DVD)
     A            DUREEF    R     D  O  8 24REFFLD(DUREE DVD)
     A                                  4 14'Code DVD'
     A                                      COLOR(WHT)
     A                                  5  9'Titre du film'
     A                                      COLOR(WHT)
     A                                  6 12'Code genre'
     A                                      COLOR(WHT)
     A                                  7 17'Année'
     A                                      COLOR(WHT)
     A                                  8 17'Durée'
     A                                      COLOR(WHT)
     A                                 24  3'F12 = Annuler'
     A                                      COLOR(BLU)
