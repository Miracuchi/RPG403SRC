      *%METADATA                                                       *
      * %TEXT Exercice 21 - A                                          *
      *%EMETADATA                                                      *
     A                                      CA03(03)
     A                                      REF(RPG403/DVD)
     A          R FMTENR                    SFL
     A            TITRE     R        O 12 15
     A            ANNEE     R        O 12 60
     A            CODDVD    R        H
     A            OPT            1   I 12  9VALUES('2' '5')
     A                                      DSPATR(PC)
     A                                      CHECK(AB)
     A          R FMTCTL                    SFLCTL(FMTENR)
     A  32                                  SFLDSPCTL
     A  32                                  SFLDSP
     A                                      SFLPAG(5)
     A                                      SFLSIZ(6)
     A                                      CA12(12)
     A  34                                  SFLEND(*MORE)
     A N34                                  ROLLUP(38)
     A                                      OVERLAY
     A  30                                  SFLCLR
     A                                      PROTECT
     A            HAUTPAGE       5  0H      SFLSCROLL
     A            RANG           4  0H      SFLRCDNBR
     A                                 11 15'Titre du film'
     A                                      COLOR(TRQ)
     A                                  9  8'Options : 2 = Modifier   5 = Affic-
     A                                      her'
     A                                      COLOR(BLU)
     A                                 11 60'Année'
     A                                      COLOR(TRQ)
     A                                 11  8'Opt'
     A                                      COLOR(TRQ)
     A          R TOUCHES
     A                                 24  3'F3 = Exit'
     A                                      COLOR(BLU)
     A                                 24 13'F12 = Annuler'
     A                                      COLOR(BLU)
     A          R FMT2                      OVERLAY
     A                                      CA12(12)
     A                                  1 32'Recherche de films'
     A                                      COLOR(WHT)
     A                                  1  3'FMT2'
     A                                      COLOR(PNK)
     A                                  1 71DATE
     A                                      COLOR(BLU)
     A                                      EDTCDE(Y)
     A                                  4  6'Recherche des films dont le titre -
     A                                      contient'
     A                                  6  5'(saisissez un critère de recherche-
     A                                      , puis appuyez sur ENTREE)'
     A            RECHERCHE     15   I  4 49
     A          R DETAILS                   WINDOW(7 19 10 50)
     A                                      CA12(12)
     A N70                              1 12'Details d''un film'
     A  70                              1 12'Mise à jout d''un film'
     A                                  3  2'Code  :'
     A                                  4  2'Titre :'
     A                                  6  2'Année :'
     A                                  7  2'Durée :'
     A                                  9  2'F12 = Annuler'
     A                                      COLOR(BLU)
     A            CODDVD    R    5   O  3 11
     A            TITRE     R   40   B  4 11
     A N70                                  DSPATR(PR)
     A            GENRE     R    1   B  5 11
     A N70                                  DSPATR(PR)
     A            ANNEE     R    4   B  6 11
     A N70                                  DSPATR(PR)
     A            DUREE     R    3   B  7 11
     A N70                                  DSPATR(PR)
     A  42                                  ERRMSG('Rentrer un critère de reche-
     A                                      rche' 42)
     A                                  5  2'Genre :'
